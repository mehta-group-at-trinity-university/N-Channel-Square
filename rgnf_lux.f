      SUBROUTINE rgnf_lux(RVEC,LENV)
C
C     **********************************************************
C
C     LENV : desired number of randomly distributed real numbers
C            generated according to the uniform distribution in [0,1] 
C     RVEC : R4 vector of LENV elements containing the LENV produced 
C            random numbers                                
C
C     **********************************************************
C
C         Subtract-and-borrow random number generator proposed by
C         Marsaglia and Zaman, implemented by F. James with the name
C         RCARRY in 1991, and later improved by Martin Luescher
C         in 1993 to produce "Luxury Pseudorandom Numbers".
C         Fortran 77 coded by F. James, 1993
C
C   +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C  ++                                                                 ++
C  ++  Calling sequences for rgnf_lux:                                ++
C  ++                                                                 ++
C  ++      CALL rgnf_lux(RVEC,LENV) :: returns a vector RVEC of LENV  ++
C  ++                REAL(4) numbers from the interval (0,1).         ++
C  ++                                                                 ++
C  ++      CALL ranset_() :: restarts the generator taking parameters ++
C  ++                from the file i_seed_rgn, containing 25 integers ++
C  ++                INTEGER(4). A backup of the file i_seed_rgn is   ++
C  ++                produced and stored in the file i_seed_rgn_bk.   ++
C  ++                                                                 ++
C  ++      CALL ranget_() :: inverse of ranset_(). Takes the current  ++
C  ++                values of the 25 INTEGER(4) parameters from the  ++
C  ++                generator and store them in the file i_seed_rgn. ++
C  ++                                                                 ++
C   +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C  ++                                                                 ++
C  ++  THE FILE i_seed_rgn MUST BE PRESENT IN THE WORKING DIRECTORY.  ++
C  ++                                                                 ++
C   +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C
C   LUXURY LEVELS.
C   ------ ------      The available luxury levels are:
C
C  level 0  (p=24): equivalent to the original RCARRY of Marsaglia
C           and Zaman, very long period, but fails many tests.
C  level 1  (p=48): considerable improvement in quality over level 0,
C           now passes the gap test, but still fails spectral test.
C  level 2  (p=97): passes all known tests, but theoretically still
C           defective.
C  level 3  (p=223): DEFAULT VALUE.  Any theoretically possible
C           correlations have very small chance of being observed.
C  level 4  (p=389): highest possible luxury, all 24 bits chaotic.
C
C     $     debug

      DOUBLE PRECISION RVEC(LENV)
      DIMENSION SEEDS(24), ISEEDS(24), ISDEXT(25)
      PARAMETER (MAXLEV=4, LXDFLT=3)
      DIMENSION NDSKIP(0:MAXLEV)
      DIMENSION NEXT(24)
      PARAMETER (TWOP12=4096., IGIGA=1000000000,JSDFLT=314159265)
      PARAMETER (ITWO24=2**24, ICONS=2147483563)
      SAVE NOTYET, I24, J24, CARRY, SEEDS, TWOM24, TWOM12, LUXLEV
      SAVE NSKIP, NDSKIP, IN24, NEXT, KOUNT, MKOUNT, INSEED
      INTEGER LUXLEV
      LOGICAL NOTYET
      DATA NOTYET, LUXLEV, IN24, KOUNT, MKOUNT /.TRUE., LXDFLT, 0,0,0/
      DATA I24,J24,CARRY/24,10,0./
      
C                                default
C  Luxury Level   0     1     2   *3*    4
      DATA NDSKIP/0,   24,   73,  199,  365 /
Corresponds to p=24    48    97   223   389
C     time factor 1     2     3     6    10   on slow workstation
C                 1    1.5    2     3     5   on fast mainframe
C
C  NOTYET is .TRUE. if no initialization has been performed yet.
C              Default Initialization by Multiplicative Congruential
      IF (NOTYET) THEN
         NOTYET = .FALSE.
         JSEED = JSDFLT  
         INSEED = JSEED
         WRITE(6,'(A,I12)') ' rgnf_lux DEFAULT INITIALIZATION: ',JSEED
         LUXLEV = LXDFLT
         NSKIP = NDSKIP(LUXLEV)
         LP = NSKIP + 24
         IN24 = 0
         KOUNT = 0
         MKOUNT = 0
         WRITE(6,'(A,I2,A,I4)')  ' rgnf_lux DEFAULT LUXURY LEVEL =  ',
     +        LUXLEV,'      p =',LP
            TWOM24 = 1.d0
         DO 25 I= 1, 24
            TWOM24 = TWOM24 * 0.5d0
         K = JSEED/53668
         JSEED = 40014*(JSEED-K*53668) -K*12211
         IF (JSEED .LT. 0)  JSEED = JSEED+ICONS
         ISEEDS(I) = MOD(JSEED,ITWO24)
   25    CONTINUE
         TWOM12 = TWOM24 * 4096.d0
         DO 50 I= 1,24
         SEEDS(I) = DBLE(ISEEDS(I))*TWOM24
         NEXT(I) = I-1
   50    CONTINUE
         NEXT(1) = 24
         I24 = 24
         J24 = 10
         CARRY = 0.d0
         IF (SEEDS(24) .EQ. 0.d0) CARRY = TWOM24
      ENDIF
C
C          The Generator proper: "Subtract-with-borrow",
C          as proposed by Marsaglia and Zaman,
C          Florida State University, March, 1989
C
      DO 100 IVEC= 1, LENV
         UNI = SEEDS(J24) - SEEDS(I24) - CARRY 
         IF (UNI .LT. 0.d0)  THEN
            UNI = UNI + 1.0d0
            CARRY = TWOM24
         ELSE
            CARRY = 0.d0
         ENDIF
         SEEDS(I24) = UNI
         I24 = NEXT(I24)
         J24 = NEXT(J24)
         RVEC(IVEC) = UNI
C     small numbers (with less than 12 "significant" bits) are "padded".
         IF (UNI .LT. TWOM12)  THEN
            RVEC(IVEC) = RVEC(IVEC) + TWOM24*SEEDS(J24)
C     and zero is forbidden in case someone takes a logarithm
            IF (RVEC(IVEC) .EQ. 0.d0)  RVEC(IVEC) = TWOM24*TWOM24
         ENDIF
C     Skipping to luxury.  As proposed by Martin Luscher.
         IN24 = IN24 + 1
         IF (IN24 .EQ. 24)  THEN
            IN24 = 0
            KOUNT = KOUNT + NSKIP
            DO 90 ISK= 1, NSKIP
               UNI = SEEDS(J24) - SEEDS(I24) - CARRY
               IF (UNI .LT. 0.d0)  THEN
                  UNI = UNI + 1.0d0
                  CARRY = TWOM24
               ELSE
                  CARRY = 0.d0
               ENDIF
               SEEDS(I24) = UNI
               I24 = NEXT(I24)
               J24 = NEXT(J24)
 90         CONTINUE
         ENDIF
 100  CONTINUE
      KOUNT = KOUNT + LENV
      IF (KOUNT .GE. IGIGA)  THEN
         MKOUNT = MKOUNT + 1
         KOUNT = KOUNT - IGIGA
      ENDIF
      RETURN
C     
C     **********************************************************
      
      ENTRY ranset_()
C     FILE :: i_seed_rng ----------> FILE :: i_seed_rng_bk 
C     FILE :: i_seed_rng ----------> PROGRAM :: rgnf_lux

      OPEN(UNIT=7,FILE='i_seed_rng',STATUS='OLD',FORM='FORMATTED')
      READ(7,'(SS,(1X,I11,1X))') ISDEXT
      CLOSE(UNIT=7)
      OPEN(UNIT=7,FILE='i_seed_rng_bk',STATUS='UNKNOWN',
     &     FORM='FORMATTED')
      WRITE(7,'(SS,(1H ,I11,1H ))') ISDEXT
      CLOSE(UNIT=7)

         NOTYET = .FALSE.
         TWOM24 = 1.d0
         DO 195 I= 1, 24
         NEXT(I) = I-1
  195    TWOM24 = TWOM24 * 0.5d0
         NEXT(1) = 24
         TWOM12 = TWOM24 * 4096.d0
      WRITE(6,'(A)') 
     &        ' FULL INITIALIZATION OF rgnf_lux WITH 25 INTEGERS:'
      WRITE(6,'(5X,5I12)') ISDEXT
      DO 200 I= 1, 24
      SEEDS(I) = DBLE(ISDEXT(I))*TWOM24
  200 CONTINUE
      CARRY = 0.d0
      IF (ISDEXT(25) .LT. 0)  CARRY = TWOM24
      ISD = IABS(ISDEXT(25))
      I24 = MOD(ISD,100)
      ISD = ISD/100
      J24 = MOD(ISD,100)
      ISD = ISD/100
      IN24 = MOD(ISD,100)
      ISD = ISD/100
      LUXLEV = ISD
        IF (LUXLEV .LE. MAXLEV) THEN
          NSKIP = NDSKIP(LUXLEV)
          WRITE (6,'(A,I2)') 
     &         ' rgnf_lux LUXURY LEVEL SET BY ranset_ TO: ',
     &                         LUXLEV
        ELSE  IF (LUXLEV .GE. 24) THEN
          NSKIP = LUXLEV - 24
          WRITE (6,'(A,I5)') 
     &         ' rgnf_lux P-VALUE SET BY ranset_ TO:',LUXLEV
        ELSE
          NSKIP = NDSKIP(MAXLEV)
          WRITE (6,'(A,I5)') ' rgnf_lux ILLEGAL LUXURY ranset_: ',LUXLEV
          LUXLEV = MAXLEV
        ENDIF
      INSEED = -1
      RETURN
C
C     **********************************************************

      ENTRY ranget_()
C     PROGRAM :: rgnf_lux ----------> FILE :: i_seed_rng

      DO 300 I= 1, 24
         ISDEXT(I) = INT(SEEDS(I)*TWOP12*TWOP12)
  300 CONTINUE
      ISDEXT(25) = I24 + 100*J24 + 10000*IN24 + 1000000*LUXLEV
      IF (CARRY .GT. 0.d0)  ISDEXT(25) = -ISDEXT(25)
      OPEN(UNIT=7,FILE='i_seed_rng',STATUS='UNKNOWN',FORM='FORMATTED')
      WRITE(7,'(SS,(1H ,I11,1H ))') ISDEXT
      CLOSE(UNIT=7)
      RETURN
C
C     **********************************************************

C                    Entry to output the "convenient" restart point
      ENTRY RLUXAT(LOUT,INOUT,K1,K2)
      LOUT = LUXLEV
      INOUT = INSEED
      K1 = KOUNT
      K2 = MKOUNT
      RETURN
C
C     **********************************************************

C                    Entry to initialize from one or three integers
      ENTRY RLUXGO(LUX,INS,K1,K2)
         IF (LUX .LT. 0) THEN
            LUXLEV = LXDFLT
         ELSE IF (LUX .LE. MAXLEV) THEN
            LUXLEV = LUX
         ELSE IF (LUX .LT. 24 .OR. LUX .GT. 2000) THEN
            LUXLEV = MAXLEV
            WRITE (6,'(A,I7)') ' rgnf_lux ILLEGAL LUXURY RLUXGO: ',LUX
         ELSE
            LUXLEV = LUX
            DO 310 ILX= 0, MAXLEV
              IF (LUX .EQ. NDSKIP(ILX)+24)  LUXLEV = ILX
  310       CONTINUE
         ENDIF
      IF (LUXLEV .LE. MAXLEV)  THEN
         NSKIP = NDSKIP(LUXLEV)
         WRITE(6,'(A,I2,A,I4)') 
     &        ' rgnf_lux LUXURY LEVEL SET BY RLUXGO :',
     &        LUXLEV,'     P=', NSKIP+24
      ELSE
          NSKIP = LUXLEV - 24
          WRITE (6,'(A,I5)') 
     &         ' rgnf_lux P-VALUE SET BY RLUXGO TO:',LUXLEV
      ENDIF
      IN24 = 0
      IF (INS .LT. 0)  WRITE (6,'(A)')   
     +   ' Illegal initialization by RLUXGO, negative input seed'
      IF (INS .GT. 0)  THEN
        JSEED = INS
        WRITE(6,'(A,3I12)') 
     &       ' rgnf_lux INITIALIZED BY RLUXGO FROM SEEDS',
     &      JSEED, K1,K2
      ELSE
        JSEED = JSDFLT
        WRITE(6,'(A)')
     &       ' rgnf_lux INITIALIZED BY RLUXGO FROM DEFAULT SEED'
      ENDIF
      INSEED = JSEED
      NOTYET = .FALSE.
      TWOM24 = 1.d0
         DO 325 I= 1, 24
           TWOM24 = TWOM24 * 0.5d0
         K = JSEED/53668
         JSEED = 40014*(JSEED-K*53668) -K*12211
         IF (JSEED .LT. 0)  JSEED = JSEED+ICONS
         ISEEDS(I) = MOD(JSEED,ITWO24)
  325    CONTINUE
      TWOM12 = TWOM24 * 4096.d0
         DO 350 I= 1,24
         SEEDS(I) = DBLE(ISEEDS(I))*TWOM24
         NEXT(I) = I-1
  350    CONTINUE
      NEXT(1) = 24
      I24 = 24
      J24 = 10
      CARRY = 0.d0
      IF (SEEDS(24) .EQ. 0.d0) CARRY = TWOM24
C        If restarting at a break point, skip K1 + IGIGA*K2
C        Note that this is the number of numbers delivered to
C        the user PLUS the number skipped (if luxury .GT. 0).
      KOUNT = K1
      MKOUNT = K2
      IF (K1+K2 .NE. 0)  THEN
        DO 500 IOUTER= 1, K2+1
          INNER = IGIGA
          IF (IOUTER .EQ. K2+1)  INNER = K1
          DO 450 ISK= 1, INNER
            UNI = SEEDS(J24) - SEEDS(I24) - CARRY 
            IF (UNI .LT. 0.d0)  THEN
               UNI = UNI + 1.0d0
               CARRY = TWOM24
            ELSE
               CARRY = 0.d0
            ENDIF
            SEEDS(I24) = UNI
            I24 = NEXT(I24)
            J24 = NEXT(J24)
  450     CONTINUE
  500   CONTINUE
C         Get the right value of IN24 by direct calculation
        IN24 = MOD(KOUNT, NSKIP+24)
        IF (MKOUNT .GT. 0)  THEN
           IZIP = MOD(IGIGA, NSKIP+24)
           IZIP2 = MKOUNT*IZIP + IN24
           IN24 = MOD(IZIP2, NSKIP+24)
        ENDIF
C       Now IN24 had better be between zero and 23 inclusive
        IF (IN24 .GT. 23) THEN
           WRITE (6,'(A/A,3I11,A,I5)')  
     +    '  Error in RESTARTING with RLUXGO:','  The values', INS,
     +     K1, K2, ' cannot occur at luxury level', LUXLEV
           IN24 = 0
        ENDIF
      ENDIF
      RETURN
      END
